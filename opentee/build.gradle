apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "fi.aalto.ssg.opentee.openteeandroid"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"

        ndk{
            moduleName "nativelibtee"
            //stl "stlport_static"
        }
    }

    publishNonDefault true  // Publish all variants (flavor / build type) of library

    sourceSets.main {
        jni.srcDirs = []    // This prevents the autogeneration of _Android.mk
        jniLibs.srcDirs 'src/main/libs'
    }

    // Task for building JNI
    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all',
                'NDK_DEBUG=1'
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = android.ndkDirectory
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    productFlavors {
        fat {
            dimension "abi"
            ndk {
                abiFilters "armeabi", "armeabi-v7a", "x86"
                versionCode = 0;
            }
            minSdkVersion 21
            targetSdkVersion 22
        }

        arm {
            dimension "abi"
            ndk {
                abiFilter "armeabi"
                versionCode = 1;
            }
            minSdkVersion 21
            targetSdkVersion 22
        }
        armv7a {
            dimension "abi"
            ndk {
                abiFilter "armeabi-v7a"
                versionCode = 3;
            }
            minSdkVersion 21
            targetSdkVersion 22
        }
        x86 {
            dimension "abi"
            ndk {
                abiFilter "x86"
                versionCode = 6;
            }
            minSdkVersion 21
            targetSdkVersion 22
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable = true
            jniDebuggable = true
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:+'
    compile "com.google.protobuf:protobuf-java:2.6.1"
    compile project(':otclient')
}
